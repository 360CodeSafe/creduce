#!@perl@
## -*- mode: Perl -*-
##
## Copyright (c) 2012 The University of Utah
## All rights reserved.
##
## This file is distributed under the University of Illinois Open Source
## License.  See the file COPYING for details.

######################################################################
#
# This is a generic Delta debugger that is parameterized by an
# interestingness test implemented as a shell script and a collection
# of transformation operators implemented as Perl modules.
#
####################################################################

use strict;
use warnings;

use FindBin;
use lib $FindBin::Bin, '@perllibdir@';
use Exporter::Lite;
use File::Basename;
use File::Which;
use POSIX;
use Regexp::Common;
use Benchmark::Timer;
use Cwd;
use File::Temp;

use creduce_config qw(PACKAGE_STRING);
use creduce_utils;

######################################################################

# if set, ensure the delta test succeeds before starting each pass
my $SANITY = 0;

# if set, show a nice ascii spinner that tells us how quickly failing
# delta tests are happening
my $SPINNER = 0;

# if set, show what each transformation did
my $DIFFS = 0;

# if set, cache results (recommended)
my $CACHE = 1;

######################################################################

my $orig_file_size;

sub print_pct ($) {
    (my $l) = @_;
    my $pct = 100 - ($l*100.0/$orig_file_size);
    printf "(%.1f %%, $l bytes)\n", $pct;
}

# these are set at startup time and never change
my $test;
my $trial_num = 0;   

my $toreduce;
my $toreduce_orig;
my $toreduce_base;
my $dir_base;
my $suffix;

my $ORIG_DIR;

# global invariant: this filename always points to the best delta
# variant we've seen so far
my $toreduce_bak;

######################################################################

my @suffixes = (".c", ".C", ".cc", ".cpp", ".CPP", ".c++", ".cp", ".cxx");

sub run_test ($) {
    (my $fn) = @_;
    my $res = runit "$test $fn >/dev/null 2>&1";
    return ($res == 0);
}

my $good_cnt;
my $bad_cnt;
my $pass_num = 0;
my %method_worked = ();
my %method_failed = ();
my $old_size = 1000000000;

sub sanity_check () {
    print "sanity check... " if $VERBOSE;

    my $tmpdir = File::Temp::tempdir( CLEANUP => 1);
    chdir $tmpdir or die;
    system "cp $toreduce_bak $toreduce";

    my $res = run_test ($toreduce);
    if (!$res) {
	die "test (and sanity check) fails";
    }
    print "successful\n" if $VERBOSE;
    chdir $ORIG_DIR or die;
    File::Path::rmtree($tmpdir);
}

my %cache = ();
my $cache_hits = 0;
my $test_cnt = 0;

my $cur_key = 0;
sub spinner() {
    my @chars = ("-", "\\", "|", "/");
    my $backsp = "\b";
    if ($cur_key == @chars) {
	$cur_key = 0;
    }
    print $backsp . $chars[$cur_key];
    $cur_key++;
}

sub delta_test ($$$$) {
    (my $method, my $arg, my $state, my $fn) = @_;
    my $prog = read_file($fn);
    my $len = length ($prog);

    print "[$pass_num $method :: $arg s:$good_cnt f:$bad_cnt] " 
	if $VERBOSE;

    if ($SPINNER) {
	spinner();
    }

    my $result;
    if ($CACHE) {
	$result = $cache{$len}{$prog};
    }

    if (defined($result)) {
	$cache_hits++;
    } else {    
	$result = run_test ($fn);
	if ($CACHE) {
	    $cache{$len}{$prog} = $result;
	}
    }

    $test_cnt++;
    
    if ($result) {
	print "\b" if $SPINNER;
	print "success " if $VERBOSE;
	print_pct(-s $fn);
	$good_cnt++;
	$method_worked{$method}{$arg}++;
	my $size = length ($prog);
	if ($CACHE && ($size < $old_size)) {
	    foreach my $k (keys %cache) {
		if ($k > ($size + 5000)) {
		    $cache{$k} = ();
		}
	    }
	}
	$old_size = $size;
	return 1;
    } else {
	print "\b" if $SPINNER;
	print "failure\n" if $VERBOSE;
	$bad_cnt++;
	$method_failed{$method}{$arg}++;
	return 0;
    }
}

sub call_prereq_check ($) {
    (my $method) = @_;
    my $str = $method."::check_prereqs";
    no strict "refs";
    if (!(&${str}())) {
	die "prereqs not found for pass $method";
    }
    print "successfully checked prereqs for $method\n" if $VERBOSE;
}

sub call_new ($$$) {
    (my $method,my $fn,my $arg) = @_;    
    my $str = $method."::new";
    no strict "refs";
    return &${str}($fn,$arg);
}

sub call_advance ($$$$) {
    (my $method,my $fn,my $arg,my $state) = @_;    
    my $str = $method."::advance";
    no strict "refs";
    return &${str}($fn,$arg,$state);
}

sub call_method ($$$$) {
    (my $method,my $fn,my $arg,my $state) = @_;    
    my $str = $method."::transform";
    no strict "refs";
    return &${str}($fn,$arg,$state);
}

# invariant: parallel execution does not escape this function

sub delta_pass ($) {
    (my $mref) = @_;    
    my $delta_method = ${$mref}{"name"};
    my $delta_arg = ${$mref}{"arg"};
    $good_cnt = 0;
    $bad_cnt = 0;

    print "\n" if $VERBOSE;
    print "===< $delta_method :: $delta_arg >===\n";

    my $tmpfn = $toreduce;
    system "cp $toreduce_bak $tmpfn";
    my $state = call_new ($delta_method,$tmpfn,$delta_arg);

    if ($SANITY) {
	sanity_check();
    }

    while (1) {
	my $tmpdir = File::Temp::tempdir( CLEANUP => 1);
	chdir $tmpdir or die;
	system "cp $toreduce_bak $tmpfn";

	(my $res, $state) = call_method ($delta_method,$tmpfn,$delta_arg,$state);
	return if ($res == $STOP);
	die unless ($res == $OK);
	system "diff $toreduce_bak $tmpfn" if $DIFFS;
	if (delta_test ($delta_method,$delta_arg,$state,$tmpfn)) {
	    # now that the delta test succeeded, this becomes our new
	    # best version
	    system "cp $tmpfn $toreduce_bak";
	} else {
	    $state = call_advance ($delta_method,$tmpfn,$delta_arg,$state);
	}

	chdir $ORIG_DIR or die;
	File::Path::rmtree ($tmpdir);
    }
}

sub usage() {
    print "usage: creduce test_script.sh file.c\n";
    die;
}

my @all_methods = ();

sub by_first_pass_pri {
    my $pa;
    my $pb;
    foreach my $mref (@all_methods) {
	my %m = %{$mref};
	if (${$a}{"name"} eq $m{"name"} && ${$a}{"arg"} eq $m{"arg"}) {
	    $pa = $m{"first_pass_pri"};
	}
	if (${$b}{"name"} eq $m{"name"} && ${$b}{"arg"} eq $m{"arg"}) {
	    $pb = $m{"first_pass_pri"};
	}
    }
    die unless defined ($pa);
    die unless defined ($pb);
    return $pa <=> $pb;
}

sub by_pri {
    my $pa;
    my $pb;

    foreach my $mref (@all_methods) {
	my %m = %{$mref};
	if (${$a}{"name"} eq $m{"name"} && ${$a}{"arg"} eq $m{"arg"}) {
	    $pa = $m{"pri"};
	}
	if (${$b}{"name"} eq $m{"name"} && ${$b}{"arg"} eq $m{"arg"}) {
	    $pb = $m{"pri"};
	}
    }
    die unless defined ($pa);
    die unless defined ($pb);
    return $pa <=> $pb;
}

sub by_last_pass_pri {
    my $pa;
    my $pb;
    foreach my $mref (@all_methods) {
	my %m = %{$mref};
	if (${$a}{"name"} eq $m{"name"} && ${$a}{"arg"} eq $m{"arg"}) {
	    $pa = $m{"last_pass_pri"};
	}
	if (${$b}{"name"} eq $m{"name"} && ${$b}{"arg"} eq $m{"arg"}) {
	    $pb = $m{"last_pass_pri"};
	}
    }
    die unless defined ($pa);
    die unless defined ($pb);
    return $pa <=> $pb;
}

sub has_pri { 
    return defined(${$_}{"pri"});
}

sub has_first_pass_pri { 
    return defined(${$_}{"first_pass_pri"});
}

sub has_last_pass_pri { 
    return defined(${$_}{"last_pass_pri"});
}

@all_methods = (
    { "name" => "pass_blank",    "arg" => "0",                                     "first_pass_pri" =>   1, },
    { "name" => "pass_lines",    "arg" => "0",                      "pri" => 410,  "first_pass_pri" =>  20,   "last_pass_pri" => 999, },
    { "name" => "pass_lines",    "arg" => "1",                      "pri" => 411,  "first_pass_pri" =>  21, },
    { "name" => "pass_lines",    "arg" => "2",                      "pri" => 412,  "first_pass_pri" =>  22, },
    { "name" => "pass_lines",    "arg" => "10",                     "pri" => 413,  "first_pass_pri" =>  23, },
    { "name" => "pass_crc",      "arg" => "",                                      "first_pass_pri" => 110, },
    { "name" => "pass_ternary",  "arg" => "b",                      "pri" => 105,  },
    { "name" => "pass_ternary",  "arg" => "c",                      "pri" => 105,  },
    { "name" => "pass_balanced", "arg" => "curly",                  "pri" => 110,  "first_pass_pri" =>  30, },
    { "name" => "pass_balanced", "arg" => "curly2",                 "pri" => 111,  "first_pass_pri" =>  31, },
    { "name" => "pass_balanced", "arg" => "curly3",                 "pri" => 112,  "first_pass_pri" =>  32, },
    { "name" => "pass_balanced", "arg" => "parens",                 "pri" => 113,  },
    { "name" => "pass_balanced", "arg" => "angles",                 "pri" => 114,  },
    { "name" => "pass_balanced", "arg" => "curly-only",             "pri" => 150,  },
    { "name" => "pass_balanced", "arg" => "parens-only",            "pri" => 151,  },
    { "name" => "pass_balanced", "arg" => "angles-only",            "pri" => 152,  },
    { "name" => "pass_clang",    "arg" => "remove-namespace",       "pri" => 200,  },
    { "name" => "pass_clang",    "arg" => "aggregate-to-scalar",    "pri" => 201,  },
   #{ "name" => "pass_clang",    "arg" => "binop-simplification",   "pri" => 201,  },
    { "name" => "pass_clang",    "arg" => "local-to-global",        "pri" => 202,  },
    { "name" => "pass_clang",    "arg" => "param-to-global",        "pri" => 203,  },
    { "name" => "pass_clang",    "arg" => "param-to-local",         "pri" => 204,  },
    { "name" => "pass_clang",    "arg" => "remove-nested-function", "pri" => 205,  },
    { "name" => "pass_clang",    "arg" => "rename-fun",                            "last_pass_pri" => 207,  },
    { "name" => "pass_clang",    "arg" => "union-to-struct",        "pri" => 208,  },
    { "name" => "pass_clang",    "arg" => "rename-param",                          "last_pass_pri" => 209,  },
    { "name" => "pass_clang",    "arg" => "rename-var",                            "last_pass_pri" => 210,  },
    { "name" => "pass_clang",    "arg" => "rename-class",                          "last_pass_pri" => 211,  },
    { "name" => "pass_clang",    "arg" => "return-void",            "pri" => 212,  },
    { "name" => "pass_clang",    "arg" => "simple-inliner",         "pri" => 213,  },
    { "name" => "pass_clang",    "arg" => "reduce-pointer-level",   "pri" => 214,  },
    { "name" => "pass_clang",    "arg" => "lift-assignment-expr",   "pri" => 215,  },
    { "name" => "pass_clang",    "arg" => "copy-propagation",       "pri" => 216,  },
    { "name" => "pass_clang",    "arg" => "callexpr-to-value",      "pri" => 217,  "first_pass_pri" => 49, },
    { "name" => "pass_clang",    "arg" => "replace-callexpr",       "pri" => 218,  "first_pass_pri" => 50, },
    { "name" => "pass_clang",    "arg" => "simplify-callexpr",      "pri" => 219,  "first_pass_pri" => 51, },
    { "name" => "pass_clang",    "arg" => "remove-unused-function", "pri" => 220,  "first_pass_pri" => 25, },
    { "name" => "pass_clang",    "arg" => "remove-unused-var",      "pri" => 221,  "first_pass_pri" => 53, },
    { "name" => "pass_clang",    "arg" => "simplify-if",            "pri" => 222,  },
    { "name" => "pass_clang",    "arg" => "reduce-array-dim",       "pri" => 223,  },
    { "name" => "pass_clang",    "arg" => "reduce-array-size",      "pri" => 224,  },
    { "name" => "pass_clang",    "arg" => "move-function-body",     "pri" => 225,  },
    { "name" => "pass_clang",    "arg" => "simplify-comma-expr",    "pri" => 226,  },
    { "name" => "pass_clang",    "arg" => "simplify-dependent-typedef",   "pri" => 227,  },
    { "name" => "pass_clang",    "arg" => "replace-simple-typedef", "pri" => 228,  },
    { "name" => "pass_clang",    "arg" => "remove-unused-field",    "pri" => 229,  },
    { "name" => "pass_clang",    "arg" => "reduce-class-template-param",  "pri" => 230,  },
    { "name" => "pass_clang",    "arg" => "remove-trivial-base-template", "pri" => 231,  },
    { "name" => "pass_clang",    "arg" => "class-template-to-class",      "pri" => 232,  },
    { "name" => "pass_clang",    "arg" => "remove-base-class",      "pri" => 233,  },
    { "name" => "pass_clang",    "arg" => "replace-derived-class",  "pri" => 234,  },
    { "name" => "pass_clang",    "arg" => "remove-unresolved-base", "pri" => 235,  },
    { "name" => "pass_clang",    "arg" => "remove-ctor-initializer","pri" => 236,  },
    { "name" => "pass_clang",    "arg" => "empty-struct-to-int",    "pri" => 237,  },
    { "name" => "pass_clang",    "arg" => "combine-global-var",                    "last_pass_pri" => 990, },
    { "name" => "pass_clang",    "arg" => "combine-local-var",                     "last_pass_pri" => 991, },
    { "name" => "pass_clang",    "arg" => "simplify-struct-union-decl",            "last_pass_pri" => 992, },
    { "name" => "pass_clang",    "arg" => "move-global-var",                       "last_pass_pri" => 993, },
    { "name" => "pass_clang",    "arg" => "unify-function-decl",                   "last_pass_pri" => 994, },
    { "name" => "pass_peep",     "arg" => "a",                      "pri" => 500,  },
    { "name" => "pass_peep",     "arg" => "b",                      "pri" => 501,  },
    { "name" => "pass_ints",     "arg" => "a",                      "pri" => 600,  },
    { "name" => "pass_ints",     "arg" => "b",                      "pri" => 601,  },
    { "name" => "pass_ints",     "arg" => "c",                      "pri" => 602,  },
    { "name" => "pass_ints",     "arg" => "d",                      "pri" => 603,  },
    { "name" => "pass_ints",     "arg" => "e",                      "pri" => 603,  },
    { "name" => "pass_indent",   "arg" => "regular",                "pri" => 1000, },
    { "name" => "pass_indent",   "arg" => "final",                                 "last_pass_pri" => 1000, },
    );

############################### main #################################

my $timer = Benchmark::Timer->new();
$timer->start();

my %prereqs_checked;
foreach my $mref (@all_methods) {
    my %method = %{$mref};
    my $mname = $method{"name"};
    die unless defined ($mname);
    next if defined ($prereqs_checked{$mname});
    $prereqs_checked{$mname} = 1;
    eval "require $mname";
    call_prereq_check($mname);
}
print "\n" if $VERBOSE;

$test = Cwd::abs_path(shift @ARGV);
usage unless defined($test);
if (!(-e $test)) {
    print "test script '$test' not found\n";
    usage();
}
if (!(-f $test)) {
    print "test script '$test' is not a plain file\n";
    usage();
}
if (!(-r $test)) {
    print "test script '$test' is not readable\n";
    usage();
}
if (!(-x $test)) {
    print "test script '$test' is not executable\n";
    usage();
}

$toreduce = shift @ARGV;
usage unless defined($toreduce);
if (!(-e $toreduce)) {
    print "'$toreduce' file not found\n";
    usage();
}
if (!(-f $toreduce)) {
    print "'$toreduce' is not a plain file\n";
    usage();
}
if (!(-r $toreduce)) {
    print "'$toreduce' is not readable\n";
    usage();
}
if (!(-w $toreduce)) {
    print "'$toreduce' is not writable\n";
    usage();
}

# Put scratch files ($toreduce_bak, $toreduce_orig) in the current
# working directory.
($toreduce_base, $dir_base, $suffix) = fileparse($toreduce,@suffixes);

$ORIG_DIR = getcwd();

# absolute path so we can refer to this file from temporary working
# dirs
$toreduce_bak  = Cwd::abs_path("$toreduce_base.bak");
$toreduce_orig = "$toreduce_base.orig";

system "cp $toreduce $toreduce_orig";
system "cp $toreduce $toreduce_bak";

my $file_size = -s $toreduce;
$orig_file_size = $file_size;

# unconditionally do this just once since otherwise output is
# confusing when the initial test fails
sanity_check();

# some passes we run first since they often make good headway quickly
print "INITIAL PASS\n" if $VERBOSE;
foreach my $method (sort by_first_pass_pri grep (has_first_pass_pri, @all_methods)) {
    delta_pass ($method);
}

# iterate to global fixpoint
print "MAIN PASSES\n" if $VERBOSE;
$file_size = -s $toreduce;

while (1) {
    foreach my $method (sort by_pri grep (has_pri, @all_methods)) {
	delta_pass ($method);
    }
    $pass_num++;
    my $s = -s $toreduce_bak;
    print "Termination check: size was $file_size; now $s\n";
    last if ($s >= $file_size);
    $file_size = $s;
}

# some passes we run last since they work best as cleanup
print "CLEANUP PASS\n" if $VERBOSE;
foreach my $method (sort by_last_pass_pri grep (has_last_pass_pri, @all_methods)) {
    delta_pass ($method);
}

print "===================== done ====================\n";

print "\n";
print "pass statistics:\n";
foreach my $mref (sort @all_methods) {
    my $method = ${$mref}{"name"};
    my $arg = ${$mref}{"arg"};
    my $w = $method_worked{$method}{$arg};
    $w=0 unless defined($w);
    my $f = $method_failed{$method}{$arg};
    $f=0 unless defined($f);
    print "  method $method :: $arg worked $w times and failed $f times\n";
}

print "\n";

# this should be the only time we touch the original file
system "cp $toreduce_bak $toreduce";

print "reduced test case:\n\n";
system "cat $toreduce";

print "\n";
$timer->stop();
my $time = int($timer->result());
print "elapsed time: $time seconds\n";

######################################################################
